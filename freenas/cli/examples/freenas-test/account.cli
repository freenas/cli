# Test runner for account  category
#

# State variables shared between create and destroy functions.
_user_obj=null
_group_obj=null

function account_create() {
	print(">>> Starting account tests")
	@$(VOL_OBJECT dataset create name=${VOL_NAME + "/home"})
	maybe_barf(_success, "+account#1", "Unable to create home dataset on volume " + VOL_NAME)

	_user_obj = @$(account user create name=${TEST_USER} fullname="My Test User" home=${"/mnt/" + VOL_NAME + "/home"} password_disabled=yes)
	maybe_barf(_success, "+account#2", "Unable to create user " + TEST_USER)
	_olduid=$(_user_obj get uid)
	maybe_barf(_success, "+account#3", "Cannot get uid value for user " + TEST_USER)
	maybe_die(typeof(_olduid) == "int", "+account#4", "Type of uid for " + TEST_USER + " not an integer")
	maybe_barf(_olduid > 0, "+account#4", "Invalid uid value for user " + TEST_USER)
	@$(_user_obj set uid=100000)
	maybe_barf(_success, "+account#5", "Cannot set uid to new value for user " + TEST_USER)
	if (_success == true) {
		@$(_user_obj set uid=${_olduid})
		maybe_barf(_success, "+account#6", "Cannot restore uid value for user " + TEST_USER)
	}
	
	_group_obj = @$(account group create name=${TEST_GROUP})
	maybe_barf(_success, "+account#7", "Unable to create group " + TEST_GROUP)
	_oldgid=$(_group_obj get gid)
	maybe_barf(_success, "+account#8", "Cannot get gid value for group " + TEST_GROUP)
	maybe_die(typeof(_oldgid) == "int", "+account#9", "Type of gid for " + TEST_GROUP + " not an integer")
	maybe_barf(_oldgid > 0, "+account#10", "Invalid gid value for group " + TEST_GROUP);
	@$(_group_obj set gid=100000)
	maybe_barf(_success, "+account#11", "Cannot set gid to new value for group " + TEST_GROUP)
	if (_success == true) {
		@$(_group_obj set gid=${_oldgid})
		maybe_barf(_success, "+account#12", "Cannot restore gid value for group " + TEST_GROUP)
	}
	if (test_me_harder) {
		account_test_hard()
	}
}

function account_destroy() {
	print(">>> Finishing account tests")
	if (VOL_OBJECT != null) {
		@$(VOL_OBJECT dataset ${VOL_NAME + "/home"} delete)
	} else {
		@$(volume ${VOL_NAME} dataset ${VOL_NAME + "/home"} delete)
	}
	maybe_barf(_success, "-account#1", "Unable to delete temporary home dataset on " + VOL_NAME)
	if (_user_obj != null) {
		@$(_user_obj delete)
	} else {
		@$(account user ${TEST_USER} delete)
	}
	maybe_barf(_success, "-account#2", "Unable to delete user " + TEST_USER)
	@$(account group ${TEST_USER} delete)
	maybe_barf(_success, "-account#3", "Unable to delete private group for user " + TEST_USER)
	if (_group_obj != null) {
		@$(_group_obj delete)
	} else {
		@$(account group ${TEST_GROUP} delete)
	}
	maybe_barf(_success, "-account#4", "Unable to delete group " + TEST_GROUP)
}

function account_test_hard()
{
	_hard_count=20
	printf(">>> Starting account stress tests: %d users and groups\n", _hard_count)

	for (i in mapf(range(0, _hard_count), TEST_USER + "-%d")) {
	    @$(account user create name=${i} fullname=${"My Test User " + i} home=${sprintf("/mnt/%s/home/%s", VOL_NAME, i)} password_disabled=yes)
	    maybe_barf(_success, "+account-hard#1", "Unable to create user " + i)
	}
	_arr = $(account user show | search name ~= ${TEST_USER + "-[0-9]*"})
	maybe_barf(length(_arr) == _hard_count, "+account-hard#2", "User count of " + str(length(_arr)) + " does not match " + str(_hard_count))
	for (i in mapf(range(0, _hard_count), TEST_GROUP + "-%d")) {
	    @$(account group create name=${i})
	    maybe_barf(_success, "+account-hard#3", "Unable to create group " + i)
	}
	_arr = $(account group show | search name ~= ${TEST_GROUP + "-[0-9]*"})
	maybe_barf(length(_arr) == _hard_count, "+account-hard#3", "Group count of " + str(length(_arr)) + " does not match " + str(_hard_count))

	for (i in mapf(range(0, _hard_count), TEST_USER + "-%d")) {
	    @$(account user ${i} delete)
	    maybe_barf(_success, "+account-hard#4", "Unable to delete user " + i)
	    # XXX delete this if delete private group support goes in.
	    @$(account group ${i} delete)
	    maybe_barf(_success, "+account-hard#5", "Unable to delete group " + i)
	}

	for (i in mapf(range(0, _hard_count), TEST_GROUP + "-%d")) {
	    @$(account group ${i} delete)
	    maybe_barf(_success, "+account-hard#6", "Unable to delete group " + i)
	}
}
