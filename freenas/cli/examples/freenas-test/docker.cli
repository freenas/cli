# Test runner for docker  category
#

# State variables shared between create and destroy functions.
_alpine_container=null
_container_name="docker_test_container"
_image_name="freenas/alpine"

function docker_create() {
	print(">>> Starting docker tests")
	_alpine_container = @$(docker container create image=${_image_name} name=${_container_name} command="/bin/sh" interactive=yes)
	maybe_barf(_success, "+docker#1", "Unable to create docker container " + _container_name)
	_x = $(_alpine_container get name)
	if (_x != _container_name) {
	   maybe_barf(_success, "+docker#2", "docker container name is not " + _container_name)
	}
	_y = $(docker image show | find name ~= alpine)
	maybe_barf(_y == "freenas/alpine:latest", "+docker#3", "docker image for alpine not found")
	@$(_alpine_container start)
	maybe_barf(_success, "+docker#3", "unable to start docker container " + _container_name)
	@$(_alpine_container stop)
	maybe_barf(_success, "+docker#3", "unable to stop docker container " + _container_name)
	if (test_me_harder) {
	   container_test_hard()
	}
}

function docker_destroy() {
	print(">>> Finishing docker tests")
	if (_alpine_container != null) {
		@$(_alpine_container delete)
	} else {
		@$(docker container ${_container_name} delete)
	}
	maybe_barf(_success, "-docker#1", "unable to delete docker container " + _container_name)
}

function container_test_hard()
{
	_hard_count=20
	_container_array = []

	for (i in mapf(range(0, _hard_count), _container_name + "%d")) {
	    _x = @$(docker container create image=${_image_name} name=${i} command="/bin/sh" interactive=yes)
	    if (not (maybe_barf(_success, "+docker-hard#1", "Unable to create container " + i))) {
	     	append(_container_array, _x)
	    }
	}

	for (i in _container_array) {
	    _n = $(i get name)
	    @$(i delete)
	    maybe_barf(_success, "+docker-hard#2", "Unable to delete container " + _n)
	}
}


